Query option 1- Gather all devices IP/ Names:
DeviceNetworkInfo
| summarize arg_max(Timestamp, IPAddresses) by DeviceName
| project DeviceName, IPAddresses

Query option 2- Target to a specific group of users you already have and wish to investigate:
DeviceNetworkInfo
| where DeviceName has_any (
    "lapbqjbtb4","lap9zhbtb4","lapdmv9tb4","lap6bf9tb4","lapc6pbtb4",
    "lapbdk9tb4","lapdhfbtb4","lap9c4btb4","lap3bq9tb4","lap6k4r5x3",
    "laphb7gp34","lap2l4btb4","lapfd4btb4","lap2tjbtb4","laphl0btb4",
    "lap2bl9tb4","lap7djbtb4","lap23w9tb4","lap4snbtb4","lap9dg9tb4",
    "lapjfq9tb4","lap3m0btb4","lapjj0btb4","lap56jbtb4","lap9rjbtb4"
)
| summarize arg_max(Timestamp, IPAddresses) by DeviceName
| project DeviceName, IPAddresses

Query option 3- Check a list of devices and gather their KBs missing + confirm they're missing enough to be non compliant.

DeviceTvmSoftwareVulnerabilities
// Filter for laptop naming convention immediately
| where DeviceName has_any (
    "lapbqjbtb4","lap9zhbtb4","lapdmv9tb4","lap6bf9tb4","lapc6pbtb4",
    "lapbdk9tb4","lapdhfbtb4","lap9c4btb4","lap3bq9tb4","lap6k4r5x3",
    "laphb7gp34","lap2l4btb4","lapfd4btb4","lap2tjbtb4","laphl0btb4",
    "lap2bl9tb4","lap7djbtb4","lap23w9tb4","lap4snbtb4","lap9dg9tb4",
    "lapjfq9tb4","lap3m0btb4","lapjj0btb4","lap56jbtb4","lap9rjbtb4"
    )
| where OSPlatform == "Windows11" and SoftwareName == "windows_11"
| where isnotempty(RecommendedSecurityUpdate)
| summarize 
    MissingPatchCount = dcount(RecommendedSecurityUpdate), 
    MissingKBs = make_set(RecommendedSecurityUpdate)
    by DeviceId, DeviceName
| where MissingPatchCount >= 2
| join kind=inner (
    DeviceNetworkInfo
    | summarize arg_max(Timestamp, *) by DeviceId 
    | project DeviceId, IPAddresses, Timestamp
) on DeviceId
| project DeviceName, MissingPatchCount, MissingKBs, IPAddresses, LastSeen = Timestamp, DeviceId
| sort by MissingPatchCount desc

Query option 4- Gather ALL devices missing 2 or more months:

DeviceTvmSoftwareVulnerabilities
// Filter laptop naming convention
| where DeviceName has_any (
    "lapbqjbtb4","lap9zhbtb4","lapdmv9tb4","lap6bf9tb4","lapc6pbtb4",
    "lapbdk9tb4","lapdhfbtb4","lap9c4btb4","lap3bq9tb4","lap6k4r5x3",
    "laphb7gp34","lap2l4btb4","lapfd4btb4","lap2tjbtb4","laphl0btb4",
    "lap2bl9tb4","lap7djbtb4","lap23w9tb4","lap4snbtb4","lap9dg9tb4",
    "lapjfq9tb4","lap3m0btb4","lapjj0btb4","lap56jbtb4","lap9rjbtb4"
    )
| where OSPlatform == "Windows11" and SoftwareName == "windows_11"
| where isnotempty(RecommendedSecurityUpdate)
| summarize 
    MissingPatchCount = dcount(RecommendedSecurityUpdate),
    MissingKBs = make_set(RecommendedSecurityUpdate)
    by DeviceId, DeviceName
| where MissingPatchCount >= 2
// Join DeviceNetworkInfo for IP + LastSeen
| join kind=inner (
    DeviceNetworkInfo
    | summarize arg_max(Timestamp, *) by DeviceId
    | project DeviceId, IPAddresses, LastSeen = Timestamp
) on DeviceId
// Join DeviceInfo for loggedâ€‘on users etc.
| join kind=inner (
    DeviceInfo
    | summarize arg_max(Timestamp, *) by DeviceId
    | project DeviceId, LoggedOnUsers
) on DeviceId
| project 
    DeviceName,
    DeviceId,
    MissingPatchCount,
    MissingKBs,
    IPAddresses,
    LoggedOnUsers,
    LastSeen
| sort by MissingPatchCount desc
| extend DaysSinceSeen = datetime_diff('day', now(), LastSeen)

